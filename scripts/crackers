#!/usr/local/bin/rebol -qs

REBOL [
	author: "Gregory Higley"
	title: "Crackers"
	needs: [
		2.100.99
		http://r3.revolucent.net/net.revolucent.series.v1.r	1.8.1	
	]
]

rules: context [
	digit: map-each d range [0 - 9] [to string! d]
	interpolate at digit 2 '|
	octet: [1 3 digit]
	address: [octet "." octet "." octet "." octet]
]

extract-ip: funct [
	string [string!]
	/local ip
][
	while [not tail? string] [
		if all [
			parse string [rules/digit thru end]
			parse string [copy ip rules/address thru end]
			tuple-ip: attempt [to tuple! ip]
			not equal? 0.0.0.0 tuple-ip
		][
			return ip
		]
		string: next string
	]
	none
]

ips: make map! []
foreach line read/lines %/var/log/auth.log [
	if ip: extract-ip line [
		unless lines: ips/:ip [
			ips/:ip: lines: copy []
		]
		switch length? lines [
			0 [repend lines [line line]]
			2 [
				remove back tail lines
				append lines line
			]
		]
	]
]

format-ip: funct [
	ip [string! tuple!]
][
	unless tuple? ip [ip: to tuple! ip]
	formatting: [-3]
	result: copy []
	repeat n 4 [
		append result format/pad formatting ip/:n #"0"
	]
	interpolate at result 2 "."
	rejoin result
]

occurrences: ["Α" "Ω"]
foreach [ip lines] ips [
	occurrence: 1
	foreach line lines [
		print reword "$IP OCCURRENCE $OCCURRENCE: $LINE" reduce ['ip format-ip ip 'occurrence occurrences/:occurrence 'line line]
		++ occurrence
	]
]

